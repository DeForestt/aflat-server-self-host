.needs <std>
import Result from "Utils/Result";
import Map from "Utils/Map";
import Server from "HTTP/Server";
import GET, POST, PUT, DELETE from "HTTP/Endpoints";
import Middleware from "HTTP/Middleware";
import HTTPMessage, HTTPResponce from "HTTP";
import Endpoints from "HTTP/Endpoint";
import Content from "Web/Content";
import {methodToString} from "HTTP" under http;
import {print} from "String" under str;
import {accept, reject} from "Utils/Result" under res;

import Index, Example, GettingStarted, InstallScript from "./src/UI/Index";

class AflatServer signs Server {

	HTTPMessage logRequest(HTTPMessage req) : Middleware {
		str.print(`[{http.methodToString(req.method)}] {req.endpoint}\n`);	
	};

	Index indexHTMX = new Index();
	Example exampleHTMX = new Example();
	GettingStarted gettingStartedHTMX = new GettingStarted();
	InstallScript installScriptSH = new InstallScript();

	Result example() : GET("/examples") {
		let resp = new HTTPResponce(my.exampleHTMX.render(), "200", "OK");
		resp.addHeader("Content-Type", "text/html");
		return res.accept(resp);
	};

	Result gettingStarted() : GET("/getting-started") {
		let resp = new HTTPResponce(my.gettingStartedHTMX.render(), "200", "OK");
		resp.addHeader("Content-Type", "text/html");
		return res.accept(resp);
	};

	Result installScript() : GET("/install.sh") {
		let resp = new HTTPResponce(my.installScriptSH.render(), "200", "OK");
		resp.addHeader("Content-Type", "text/html");
		return res.accept(resp);
	};
	
	Map index() : Endpoints("/") {
		return {
			"GET": [AflatServer my] => {
				let resp = new HTTPResponce(my.indexHTMX.render(), "200", "OK");
				resp.addHeader("Content-Type", "text/html");
				return res.accept(resp);
			}
		};
	};

	AflatServer init() {
		return my;
	};
};


int main() {
	let server = AflatServer();
	server.listen();
	return 0;
};
